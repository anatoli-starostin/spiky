check_detectors_logic(
    EXTERNAL_REAL_DT* r_input,
    uint32_t n_inputs,
    AnchorsPair* r_detectors,
    uint32_t n_detectors,
    uint32_t n_anchors_per_detector,
    int32_t* w_lookup_indices,
    EXTERNAL_REAL_DT* w_min_anchor_deltas,
    int32_t* w_min_anchor_delta_indices
) {
    uint32_t i = blockIdx.x * blockDim.x + threadIdx.x;

    if(i < n_detectors) {
        // Offset input for current batch (sequence_length == 1)
        r_input += blockIdx.y * n_inputs;
        w_lookup_indices += blockIdx.y * n_detectors + i;
        w_min_anchor_deltas += blockIdx.y * n_detectors + i;
        w_min_anchor_delta_indices += blockIdx.y * n_detectors + i;

        // Get detector's anchor pairs
        AnchorsPair* detector_anchors = r_detectors + i * n_anchors_per_detector;

        // Calculate differences between anchor pairs and form bit representation
        int32_t lookup_index = 0;
        EXTERNAL_REAL_DT min_delta = 0.0;
        int32_t min_delta_anchor_idx = -1;
        AnchorsPair ap;

        for(uint32_t anchor_idx = 0; anchor_idx < n_anchors_per_detector; anchor_idx++) {
            ap = detector_anchors[anchor_idx];

            // Skip if anchor IDs are invalid
            if(ap.anchor1_id == std::numeric_limits<NeuronIndex_t>::max() ||
               ap.anchor2_id == std::numeric_limits<NeuronIndex_t>::max()) {
                continue;
            }

            EXTERNAL_REAL_DT delta = r_input[ap.anchor1_id] - r_input[ap.anchor2_id];

            // Form bit representation: 1 if delta > 0, 0 if delta <= 0
            if(delta > 0.0) {
                lookup_index |= (1 << anchor_idx);
            }

            // Track anchor pair with minimum absolute delta
            if((min_delta_anchor_idx == -1) || (fabs(delta) < fabs(min_delta))) {
                min_delta = delta;
                min_delta_anchor_idx = anchor_idx;
            }
        }

        // Store lookup index and related data
        *w_lookup_indices = lookup_index;
        *w_min_anchor_deltas = min_delta;
        *w_min_anchor_delta_indices = min_delta_anchor_idx;

        __SUPER_DETAILED_TRACE__("check_detectors_logic: detector_index %d, lookup_index %d, min_delta %f, min_delta_anchor_idx %d\n", i, lookup_index, min_delta, min_delta_anchor_idx);
    }
}

fire_detectors_logic(
    EXTERNAL_REAL_DT* r_input,
    uint32_t n_inputs,
    AnchorsPair* r_detectors,
    uint32_t n_detectors,
    uint32_t n_anchors_per_detector,
    uint32_t n_lookup_neurons_per_detector,
    int32_t* w_lookup_indices,
    EXTERNAL_REAL_DT* w_min_anchor_deltas,
    int32_t* w_min_anchor_delta_indices,
    NoDelaysIndexedSynapsesInfo* r_lookup_neuron_synapses_infos,
    Firing* rw_firings_buffer,
    uint64_t* rw_firings_counter_ptr,
    uint32_t synapse_group_size,
    uint8_t* lut_data,
    int device
) {
    uint32_t tid = threadIdx.x;
    uint32_t i = blockIdx.x * blockDim.x + tid; // blockDim.x should be power of 2

    NoDelaysIndexedSynapsesInfo lookup_neuron_synapses_info;
    lookup_neuron_synapses_info.n_groups = 0;

    if(i < n_detectors) {
        // Offset input for current batch (sequence_length == 1)
        r_input += blockIdx.y * n_inputs;
        w_lookup_indices += blockIdx.y * n_detectors + i;
        w_min_anchor_deltas += blockIdx.y * n_detectors + i;
        w_min_anchor_delta_indices += blockIdx.y * n_detectors + i;

        // Get detector's anchor pairs
        AnchorsPair* detector_anchors = r_detectors + i * n_anchors_per_detector;

        // Calculate differences between anchor pairs and form bit representation
        int32_t lookup_index = 0;
        EXTERNAL_REAL_DT min_delta = 0.0;
        int32_t min_delta_anchor_idx = -1;
        AnchorsPair ap;

        for(uint32_t anchor_idx = 0; anchor_idx < n_anchors_per_detector; anchor_idx++) {
            ap = detector_anchors[anchor_idx];

            // Skip if anchor IDs are invalid
            if(ap.anchor1_id == std::numeric_limits<NeuronIndex_t>::max() ||
               ap.anchor2_id == std::numeric_limits<NeuronIndex_t>::max()) {
                continue;
            }

            EXTERNAL_REAL_DT delta = r_input[ap.anchor1_id] - r_input[ap.anchor2_id];

            // Form bit representation: 1 if delta > 0, 0 if delta <= 0
            if(delta > 0.0) {
                lookup_index |= (1 << anchor_idx);
            }

            // Track anchor pair with minimum absolute delta
            if((min_delta_anchor_idx == -1) || (fabs(delta) < fabs(min_delta))) {
                min_delta = delta;
                min_delta_anchor_idx = anchor_idx;
            }
        }

        // Store lookup index and related data
        *w_lookup_indices = lookup_index;
        *w_min_anchor_deltas = min_delta;
        *w_min_anchor_delta_indices = min_delta_anchor_idx;

        __SUPER_DETAILED_TRACE__("fire_detectors_logic: detector_index %d, lookup_index %d, min_delta %f, min_delta_anchor_idx %d\n", i, lookup_index, min_delta, min_delta_anchor_idx);

        if(rw_firings_buffer != nullptr) {
            lookup_neuron_synapses_info = *(r_lookup_neuron_synapses_infos + i * n_lookup_neurons_per_detector + lookup_index);
        }
    }

    if(rw_firings_buffer != nullptr) {
        uint64_t firings_offset;
        if(device == -1) {
            if(lookup_neuron_synapses_info.n_groups > 0) {
                firings_offset = *rw_firings_counter_ptr;
                (*rw_firings_counter_ptr) += lookup_neuron_synapses_info.n_groups;
            }
        } else {
            #ifdef ATOMIC
            extern __shared__ __align__(16) uint8_t __sm[];
            uint32_t *sdata = reinterpret_cast<uint32_t *>(__sm);
            sdata[tid] = lookup_neuron_synapses_info.n_groups;
            __syncthreads();
            uint32_t t;
            int offset;
            int idx;

            // upsweep (reduce)
            for(offset = 1; offset < blockDim.x; offset <<= 1) {
                idx = ((tid + 1) * (offset << 1)) - 1;
                if(idx < blockDim.x) {
                    t = sdata[idx - offset];
                    if(t > 0) {
                        sdata[idx] += t;
                    }
                }
                __syncthreads();
            }

            // inject global shift
            if(tid == 0) {
                sdata[blockDim.x - 1] = atomicAdd(
                    reinterpret_cast<unsigned long long*>(rw_firings_counter_ptr),
                    static_cast<unsigned long long>(sdata[blockDim.x - 1])
                );
            }
            __syncthreads();

            // downsweep
            for(offset = blockDim.x >> 1; offset > 0; offset >>= 1) {
                idx = ((tid + 1) * (offset << 1)) - 1;
                if (idx < blockDim.x) {
                    t = sdata[idx - offset];
                    sdata[idx - offset] = sdata[idx];
                    if(t > 0) {
                        sdata[idx] += t;
                    }
                }
                __syncthreads();
            }

            firings_offset = sdata[tid];
            #endif
        }

        if(lookup_neuron_synapses_info.n_groups > 0) {
            if(lookup_neuron_synapses_info.n_synapse_metas == 1) {
                NeuronDataId_t current_group_id = lookup_neuron_synapses_info.first_group_id;
                for(uint32_t j=0;j < lookup_neuron_synapses_info.n_groups;j++) {
                    rw_firings_buffer[firings_offset + j] = Firing{
                        blockIdx.y, 1.0, current_group_id
                    };
                    current_group_id += SizeOfForwardSynapseGroup(synapse_group_size, true);
                }
            } else {
                DelayInfo* delays_info = DelayInfos(lookup_neuron_synapses_info.delays_info_id, lut_data);
                for(uint32_t j=0;j < lookup_neuron_synapses_info.n_synapse_metas;j++) {
                    DelayInfo delay_info = delays_info[j];
                    if(delay_info != 0) {
                        uint32_t n_groups = DELAY_INFO_N_GROUPS(delay_info);
                        NeuronDataId_t current_group_id = lookup_neuron_synapses_info.first_group_id + DELAY_INFO_BYTE_SHIFT_FROM_FIRST_GROUP(delay_info);
                        for(uint32_t k=0;k < n_groups;k++) {
                            rw_firings_buffer[firings_offset + k] = Firing{
                                blockIdx.y, 1.0, current_group_id
                            };
                            current_group_id += SizeOfForwardSynapseGroup(synapse_group_size, true);
                        }
                        firings_offset += n_groups;
                    }
                }
            }
        }
    }
}

fill_outputs_by_forward_groups_logic(
    EXTERNAL_REAL_DT* r_weights,
    NeuronDataId_t first_synapse_id,
    Firing* r_firings_buffer,
    uint64_t n_firings,
    EXTERNAL_REAL_DT* w_output,
    uint32_t n_lookup_neurons,
    uint32_t n_outputs,
    uint8_t* lut_data,
    double int_rescaler
) {
    uint64_t i = static_cast<uint64_t>(blockIdx.x) * blockDim.x + threadIdx.x;
    if(i < n_firings) {
        Firing firing = r_firings_buffer[i];
        w_output += firing.batch_index * n_outputs;

        ForwardSynapseGroup* fws_group_ptr = GetForwardSynapseGroup(firing.data_id, lut_data);
        int32_t current_group_size = static_cast<int32_t>(SynapseGroupSize(fws_group_ptr->meta_info));
        NeuronIndex_t* current_target_ptr = TargetNeuronsInForwardGroup(firing.data_id, lut_data);
        long weight_shift = (reinterpret_cast<uint8_t *>(current_target_ptr) - (lut_data + first_synapse_id)) >> 2;
        EXTERNAL_REAL_DT* weight_ptr = r_weights + weight_shift;
        uint4 targets_quad = make_uint4(0, 0, 0, 0);
        float4 weights_quad = make_float4(0.0, 0.0, 0.0, 0.0);

        NeuronIndex_t target_id;
        double weight;
        SUMMATION32_DT payload;

        __SUPER_DETAILED_TRACE__("[fill_outputs_by_forward_groups_logic] i %d, current_group_size %d, n_firings %d, weight_shift %ld\n", i, current_group_size, n_firings, weight_shift);

        int32_t n_t_buffered = 0;
        int32_t n_w_buffered = 0;
        for(int32_t cursor=0;cursor < current_group_size;cursor++, current_target_ptr++, weight_ptr++) {
            if(((reinterpret_cast<uintptr_t>(current_target_ptr) & 15) == 0) && (cursor < current_group_size - 3)) {
                #ifdef ATOMIC
                asm volatile(
                    "ld.global.v4.s32 {%0,%1,%2,%3}, [%4];"
                    : "=r"(targets_quad.x), "=r"(targets_quad.y), "=r"(targets_quad.z), "=r"(targets_quad.w)
                    : "l"(current_target_ptr)
                );
                #else
                targets_quad = *reinterpret_cast<uint4 *>(current_target_ptr);
                #endif
                target_id = targets_quad.x;
                n_t_buffered = 3;
            } else if(n_t_buffered > 0) {
                if(n_t_buffered == 3) {
                    target_id = targets_quad.y;
                } else if(n_t_buffered == 2) {
                    target_id = targets_quad.z;
                } else {
                    target_id = targets_quad.w;
                }
                n_t_buffered--;
            } else {
                target_id = *current_target_ptr;
            }

            if(((reinterpret_cast<uintptr_t>(weight_ptr) & 15) == 0) && (cursor < current_group_size - 3)) {
                #ifdef ATOMIC
                asm volatile(
                    "ld.global.v4.f32 {%0,%1,%2,%3}, [%4];"
                    : "=f"(weights_quad.x), "=f"(weights_quad.y), "=f"(weights_quad.z), "=f"(weights_quad.w)
                    : "l"(weight_ptr)
                );
                #else
                weights_quad = *reinterpret_cast<float4 *>(weight_ptr);
                #endif
                weight = static_cast<double>(weights_quad.x);
                n_w_buffered = 3;
            } else if(n_w_buffered > 0) {
                if(n_w_buffered == 3) {
                    weight = static_cast<double>(weights_quad.y);
                } else if(n_w_buffered == 2) {
                    weight = static_cast<double>(weights_quad.z);
                } else {
                    weight = static_cast<double>(weights_quad.w);
                }
                n_w_buffered--;
            } else {
                weight = static_cast<double>(*weight_ptr);
            }

            target_id -= n_lookup_neurons;
            __SUPER_DETAILED_TRACE__("[fill_outputs_by_forward_groups_logic] target_id %d, weight %f\n", target_id, weight);

            #ifdef INTEGERS_INSTEAD_OF_FLOATS
                payload = static_cast<SUMMATION32_DT>(weight * static_cast<double>(DENOMINATOR32) * int_rescaler);
                #ifdef ATOMIC
                atomicAdd(
                    reinterpret_cast<SUMMATION32_DT *>(w_output + target_id),
                    payload
                );
                #else
                *reinterpret_cast<SUMMATION32_DT *>(w_output + target_id) += payload;
                #endif
            #else
                payload = static_cast<SUMMATION32_DT>(weight);
                #ifdef ATOMIC
                atomicAdd(
                    w_output + target_id,
                    payload
                );
                #else
                __SUPER_DETAILED_TRACE__("[fill_outputs_by_forward_groups_logic] target_id %d, payload %f\n", target_id, payload);
                w_output[target_id] += payload;
                #endif
            #endif
        }
    }
}

fill_outputs_fully_connected_logic(
    EXTERNAL_REAL_DT* r_weights,
    int32_t* r_lookup_indices,
    EXTERNAL_REAL_DT* w_output,
    uint32_t n_outputs,
    uint32_t n_detectors,
    uint32_t n_detector_blocks,
    uint32_t n_lookup_neurons_per_detector,
    int32_t n_detectors_per_block,
    double int_rescaler
) {
    uint32_t i = blockIdx.x * blockDim.x + threadIdx.x;
    if(i < n_outputs * n_detector_blocks) {
        uint32_t output_index = i / n_detector_blocks;
        uint32_t detector_block_index = i % n_detector_blocks;
        __SUPER_DETAILED_TRACE__("fill_outputs_fully_connected_logic: batch_index %d, output_index %d, detector_block_index %d\n", blockIdx.y, output_index, detector_block_index);
        r_lookup_indices += blockIdx.y * n_detectors;
        r_lookup_indices += detector_block_index * n_detectors_per_block;
        w_output += blockIdx.y * n_outputs;
        uint64_t weights_per_detector = static_cast<uint64_t>(n_lookup_neurons_per_detector) * n_outputs;
        __SUPER_DETAILED_TRACE__("fill_outputs_fully_connected_logic: weights_per_detector %lu\n", weights_per_detector);
        r_weights += weights_per_detector * detector_block_index * n_detectors_per_block;
        int4 li_quad = make_int4(0, 0, 0, 0);
        int32_t current_lookup_index;
        int32_t n_buffered = 0;
        double weight_sum = 0.0;
        if((detector_block_index == n_detector_blocks - 1) && (n_detectors % n_detectors_per_block)) {
            n_detectors_per_block = n_detectors % n_detectors_per_block;
        }
        __SUPER_DETAILED_TRACE__("fill_outputs_fully_connected_logic: n_detectors_per_block %d\n", n_detectors_per_block);
        for(int32_t cursor=0;cursor < n_detectors_per_block;cursor++, r_lookup_indices++, r_weights+=weights_per_detector) {
            if(((reinterpret_cast<uintptr_t>(r_lookup_indices) & 15) == 0) && (cursor < n_detectors_per_block - 3)) {
                #ifdef ATOMIC
                asm volatile(
                    "ld.global.v4.s32 {%0,%1,%2,%3}, [%4];"
                    : "=r"(li_quad.x), "=r"(li_quad.y), "=r"(li_quad.z), "=r"(li_quad.w)
                    : "l"(r_lookup_indices)
                );
                #else
                li_quad = *reinterpret_cast<int4 *>(r_lookup_indices);
                #endif
                current_lookup_index = li_quad.x;
                n_buffered = 3;
            } else if(n_buffered > 0) {
                if(n_buffered == 3) {
                    current_lookup_index = li_quad.y;
                } else if(n_buffered == 2) {
                    current_lookup_index = li_quad.z;
                } else {
                    current_lookup_index = li_quad.w;
                }
                n_buffered--;
            } else {
                current_lookup_index = *r_lookup_indices;
            }
            __SUPER_DETAILED_TRACE__("fill_outputs_fully_connected_logic: current_lookup_index %d, output_index %d, weight %f\n", current_lookup_index, output_index, r_weights[current_lookup_index * n_outputs + output_index]);
            weight_sum += static_cast<double>(r_weights[current_lookup_index * n_outputs + output_index]);
        }

        __SUPER_DETAILED_TRACE__("fill_outputs_fully_connected_logic: weight_sum %f\n", weight_sum);

        SUMMATION32_DT payload;
        #ifdef INTEGERS_INSTEAD_OF_FLOATS
            payload = static_cast<SUMMATION32_DT>(weight_sum * static_cast<double>(DENOMINATOR32) * int_rescaler);
            #ifdef ATOMIC
            atomicAdd(
                reinterpret_cast<SUMMATION32_DT *>(w_output + output_index),
                payload
            );
            #else
            *reinterpret_cast<SUMMATION32_DT *>(w_output + output_index) += payload;
            #endif
        #else
            payload = static_cast<SUMMATION32_DT>(weight_sum);
            #ifdef ATOMIC
            atomicAdd(
                w_output + output_index,
                payload
            );
            #else
            __SUPER_DETAILED_TRACE__("fill_outputs_fully_connected_logic: payload %f\n", payload);
            w_output[output_index] += payload;
            #endif
        #endif
    }
}

fire_detectors_by_lookup_indices_logic(
    uint32_t n_detectors,
    int32_t* r_lookup_indices,
    int32_t* r_min_anchor_delta_indices,
    uint32_t n_lookup_neurons_per_detector,
    NoDelaysIndexedSynapsesInfo* r_lookup_neuron_synapses_infos,
    Firing* rw_firings_buffer,
    uint64_t* rw_firings_counter_ptr,
    uint32_t synapse_group_size,
    uint8_t* lut_data,
    int device
) {
    uint32_t tid = threadIdx.x;
    uint32_t i = blockIdx.x * blockDim.x + tid; // blockDim.x should be power of 2

    NoDelaysIndexedSynapsesInfo chosen_lookup_neuron_synapses_info;
    NoDelaysIndexedSynapsesInfo chosen_alternative_lookup_neuron_synapses_info;
    chosen_lookup_neuron_synapses_info.n_groups = 0;
    chosen_alternative_lookup_neuron_synapses_info.n_groups = 0;

    if(i < n_detectors) {
        r_lookup_indices += blockIdx.y * n_detectors;
        r_min_anchor_delta_indices += blockIdx.y * n_detectors;

        int32_t lookup_index = r_lookup_indices[i];
        chosen_lookup_neuron_synapses_info = *(r_lookup_neuron_synapses_infos + i * n_lookup_neurons_per_detector + lookup_index);
        chosen_alternative_lookup_neuron_synapses_info = *(r_lookup_neuron_synapses_infos + i * n_lookup_neurons_per_detector + (lookup_index ^ (1 << r_min_anchor_delta_indices[i])));
    }

    uint64_t firings_offset;
    if(device == -1) {
        if((chosen_lookup_neuron_synapses_info.n_groups + chosen_lookup_neuron_synapses_info.n_groups) > 0) {
            firings_offset = *rw_firings_counter_ptr;
            (*rw_firings_counter_ptr) += chosen_lookup_neuron_synapses_info.n_groups + chosen_alternative_lookup_neuron_synapses_info.n_groups;
        }
    } else {
        #ifdef ATOMIC
        extern __shared__ __align__(16) uint8_t __sm[];
        uint32_t *sdata = reinterpret_cast<uint32_t *>(__sm);
        sdata[tid] = chosen_lookup_neuron_synapses_info.n_groups + chosen_alternative_lookup_neuron_synapses_info.n_groups;
        __syncthreads();
        uint32_t t;
        int offset;
        int idx;

        // upsweep (reduce)
        for(offset = 1; offset < blockDim.x; offset <<= 1) {
            idx = ((tid + 1) * (offset << 1)) - 1;
            if(idx < blockDim.x) {
                t = sdata[idx - offset];
                if(t > 0) {
                    sdata[idx] += t;
                }
            }
            __syncthreads();
        }

        // inject global shift
        if(tid == 0) {
            sdata[blockDim.x - 1] = atomicAdd(
                reinterpret_cast<unsigned long long*>(rw_firings_counter_ptr),
                static_cast<unsigned long long>(sdata[blockDim.x - 1])
            );
        }
        __syncthreads();

        // downsweep
        for(offset = blockDim.x >> 1; offset > 0; offset >>= 1) {
            idx = ((tid + 1) * (offset << 1)) - 1;
            if (idx < blockDim.x) {
                t = sdata[idx - offset];
                sdata[idx - offset] = sdata[idx];
                if(t > 0) {
                    sdata[idx] += t;
                }
            }
            __syncthreads();
        }

        firings_offset = sdata[tid];
        #endif
    }

    if((chosen_lookup_neuron_synapses_info.n_groups + chosen_lookup_neuron_synapses_info.n_groups) > 0) {
        if(chosen_lookup_neuron_synapses_info.n_synapse_metas == 1) {
            NeuronDataId_t current_group_id = chosen_lookup_neuron_synapses_info.first_group_id;
            for(uint32_t j=0;j < chosen_lookup_neuron_synapses_info.n_groups;j++) {
                rw_firings_buffer[firings_offset + j] = Firing{
                    blockIdx.y, 1.0, current_group_id
                };
                current_group_id += SizeOfForwardSynapseGroup(synapse_group_size, true);
            }
            firings_offset += chosen_lookup_neuron_synapses_info.n_groups;
            current_group_id = chosen_alternative_lookup_neuron_synapses_info.first_group_id;
            for(uint32_t j=0;j < chosen_alternative_lookup_neuron_synapses_info.n_groups;j++) {
                rw_firings_buffer[firings_offset + j] = Firing{
                    blockIdx.y, -1.0, current_group_id
                };
                current_group_id += SizeOfForwardSynapseGroup(synapse_group_size, true);
            }
        } else {
            DelayInfo* delays_info = DelayInfos(chosen_lookup_neuron_synapses_info.delays_info_id, lut_data);
            for(uint32_t j=0;j < chosen_lookup_neuron_synapses_info.n_synapse_metas;j++) {
                DelayInfo delay_info = delays_info[j];
                if(delay_info != 0) {
                    uint32_t n_groups = DELAY_INFO_N_GROUPS(delay_info);
                    NeuronDataId_t current_group_id = chosen_lookup_neuron_synapses_info.first_group_id + DELAY_INFO_BYTE_SHIFT_FROM_FIRST_GROUP(delay_info);
                    for(uint32_t k=0;k < n_groups;k++) {
                        rw_firings_buffer[firings_offset + k] = Firing{
                            blockIdx.y, 1.0, current_group_id
                        };
                        current_group_id += SizeOfForwardSynapseGroup(synapse_group_size, true);
                    }
                    firings_offset += n_groups;
                }
            }
            delays_info = DelayInfos(chosen_alternative_lookup_neuron_synapses_info.delays_info_id, lut_data);
            for(uint32_t j=0;j < chosen_alternative_lookup_neuron_synapses_info.n_synapse_metas;j++) {
                DelayInfo delay_info = delays_info[j];
                if(delay_info != 0) {
                    uint32_t n_groups = DELAY_INFO_N_GROUPS(delay_info);
                    NeuronDataId_t current_group_id = chosen_alternative_lookup_neuron_synapses_info.first_group_id + DELAY_INFO_BYTE_SHIFT_FROM_FIRST_GROUP(delay_info);
                    for(uint32_t k=0;k < n_groups;k++) {
                        rw_firings_buffer[firings_offset + k] = Firing{
                            blockIdx.y, -1.0, current_group_id
                        };
                        current_group_id += SizeOfForwardSynapseGroup(synapse_group_size, true);
                    }
                    firings_offset += n_groups;
                }
            }
        }
    }
}

gather_gradients_logic(
    EXTERNAL_REAL_DT* r_weights,
    NeuronDataId_t first_synapse_id,
    Firing* r_firings,
    uint64_t n_firings,
    EXTERNAL_REAL_DT* r_output_gradients,
    SUMMATION32_DT* w_before_detectors_gradients,
    EXTERNAL_REAL_DT* w_weights_gradients,
    uint32_t n_lookup_neurons,
    uint32_t n_outputs,
    uint8_t* lut_data,
    REAL_DT first_synapse_meta_lr,
    BaseSynapseMeta* r_synapse_metas,
    double int_rescaler
) {
    uint64_t i = static_cast<uint64_t>(blockIdx.x) * blockDim.x + threadIdx.x;
    if(i < n_firings) {
        Firing firing = r_firings[i];
        r_output_gradients += firing.batch_index * n_outputs;
        w_before_detectors_gradients += firing.batch_index * n_lookup_neurons;

        ForwardSynapseGroup fws_group = *GetForwardSynapseGroup(firing.data_id, lut_data);
        double lr;
        uint32_t sm_index = SynapseGroupSynapseMetaIndex(fws_group.meta_info);
        if(sm_index == 0) {
            lr = static_cast<double>(first_synapse_meta_lr);
        } else {
            lr = static_cast<double>((r_synapse_metas + sm_index)->lr);
        }

        int32_t current_group_size = static_cast<int32_t>(SynapseGroupSize(fws_group.meta_info));
        NeuronIndex_t* current_target_ptr = TargetNeuronsInForwardGroup(firing.data_id, lut_data);
        long weight_shift = (reinterpret_cast<uint8_t *>(current_target_ptr) - (lut_data + first_synapse_id)) >> 2;
        EXTERNAL_REAL_DT* weight_ptr = r_weights + weight_shift;
        uint4 targets_quad = make_uint4(0, 0, 0, 0);
        float4 weights_quad = make_float4(0.0, 0.0, 0.0, 0.0);

        NeuronIndex_t target_id;
        double weight;
        double output_grad;
        double grad_x = 0.0;
        SUMMATION32_DT sum32_delta;

        __SUPER_DETAILED_TRACE__("[gather_gradients_logic] i %d, current_group_size %d, n_firings %d, weight_shift %ld\n", i, current_group_size, n_firings, weight_shift);

        int32_t n_t_buffered = 0;
        int32_t n_w_buffered = 0;
        for(int32_t cursor=0;cursor < current_group_size;cursor++, current_target_ptr++, weight_ptr++, weight_shift++) {
            if(((reinterpret_cast<uintptr_t>(current_target_ptr) & 15) == 0) && (cursor < current_group_size - 3)) {
                #ifdef ATOMIC
                asm volatile(
                    "ld.global.v4.s32 {%0,%1,%2,%3}, [%4];"
                    : "=r"(targets_quad.x), "=r"(targets_quad.y), "=r"(targets_quad.z), "=r"(targets_quad.w)
                    : "l"(current_target_ptr)
                );
                #else
                targets_quad = *reinterpret_cast<uint4 *>(current_target_ptr);
                #endif
                target_id = targets_quad.x;
                n_t_buffered = 3;
            } else if(n_t_buffered > 0) {
                if(n_t_buffered == 3) {
                    target_id = targets_quad.y;
                } else if(n_t_buffered == 2) {
                    target_id = targets_quad.z;
                } else {
                    target_id = targets_quad.w;
                }
                n_t_buffered--;
            } else {
                target_id = *current_target_ptr;
            }

            if(((reinterpret_cast<uintptr_t>(weight_ptr) & 15) == 0) && (cursor < current_group_size - 3)) {
                #ifdef ATOMIC
                asm volatile(
                    "ld.global.v4.f32 {%0,%1,%2,%3}, [%4];"
                    : "=f"(weights_quad.x), "=f"(weights_quad.y), "=f"(weights_quad.z), "=f"(weights_quad.w)
                    : "l"(weight_ptr)
                );
                #else
                weights_quad = *reinterpret_cast<float4 *>(weight_ptr);
                #endif
                weight = static_cast<double>(weights_quad.x);
                n_w_buffered = 3;
            } else if(n_w_buffered > 0) {
                if(n_w_buffered == 3) {
                    weight = static_cast<double>(weights_quad.y);
                } else if(n_w_buffered == 2) {
                    weight = static_cast<double>(weights_quad.z);
                } else {
                    weight = static_cast<double>(weights_quad.w);
                }
                n_w_buffered--;
            } else {
                weight = static_cast<double>(*weight_ptr);
            }

            __SUPER_DETAILED_TRACE__("[gather_gradients_logic] target_id %d, weight %f\n", target_id, weight);

            target_id -= n_lookup_neurons;
            output_grad = static_cast<double>(r_output_gradients[target_id]);

            if(fabs(output_grad) > EPS) {
                grad_x += weight * output_grad;
                if(firing.payload == 1.0) {
                    #ifdef INTEGERS_INSTEAD_OF_FLOATS
                        sum32_delta = static_cast<SUMMATION32_DT>(output_grad * lr * static_cast<double>(DENOMINATOR32) * int_rescaler);
                        #ifdef ATOMIC
                        atomicAdd(
                            reinterpret_cast<SUMMATION32_DT *>(w_weights_gradients) + weight_shift,
                            sum32_delta
                        );
                        #else
                        *reinterpret_cast<SUMMATION32_DT *>(w_weights_gradients + weight_shift) += sum32_delta;
                        #endif
                    #else
                        sum32_delta = static_cast<SUMMATION32_DT>(output_grad * lr);
                        #ifdef ATOMIC
                        atomicAdd(
                            w_weights_gradients + weight_shift,
                            sum32_delta
                        );
                        #else
                        w_weights_gradients[weight_shift] += sum32_delta;
                        #endif
                    #endif
                }
            }
        }

        #ifdef INTEGERS_INSTEAD_OF_FLOATS
            sum32_delta = static_cast<SUMMATION32_DT>(grad_x * lr * static_cast<double>(DENOMINATOR32) * int_rescaler);
            #ifdef ATOMIC
            atomicAdd(
                w_before_detectors_gradients + fws_group.source_neuron_index,
                sum32_delta
            );
            #else
            w_before_detectors_gradients[fws_group.source_neuron_index] += sum32_delta;
            #endif
        #else
            sum32_delta = static_cast<SUMMATION32_DT>(grad_x * lr);
            #ifdef ATOMIC
            atomicAdd(
                w_before_detectors_gradients + fws_group.source_neuron_index,
                sum32_delta
            );
            #else
            __SUPER_DETAILED_TRACE__(
                "[gather_gradients_logic] firing.batch_index %d, fws_group.source_neuron_index %d, firing.payload %f, sum32_delta %f\n",
                firing.batch_index, fws_group.source_neuron_index, firing.payload, sum32_delta
            );
            w_before_detectors_gradients[fws_group.source_neuron_index] += sum32_delta;
            #endif
        #endif
    }
}

gather_x_gradients_fully_connected_simple_logic(
    EXTERNAL_REAL_DT* r_weights,
    EXTERNAL_REAL_DT* r_output_gradients,
    int32_t* r_lookup_indices,
    int32_t* r_min_anchor_delta_indices,
    SUMMATION32_DT* w_before_detectors_gradients,
    uint32_t n_outputs,
    uint32_t n_detectors,
    uint32_t n_lookup_neurons_per_detector,
    double lr,
    double int_rescaler
) {
    uint32_t i = blockIdx.x * blockDim.x + threadIdx.x;
    if(i < n_detectors * n_outputs) {
        uint32_t detector_index = i / n_outputs;
        uint32_t output_index = i % n_outputs;
        r_lookup_indices += blockIdx.y * n_detectors;
        int32_t lookup_index = r_lookup_indices[detector_index];
        if(r_min_anchor_delta_indices != nullptr) {
            r_min_anchor_delta_indices += blockIdx.y * n_detectors;
            lookup_index ^= (1 << r_min_anchor_delta_indices[detector_index]);
        }
        r_weights += static_cast<uint64_t>(n_lookup_neurons_per_detector) * n_outputs * detector_index;
        r_weights += n_outputs * lookup_index;
        w_before_detectors_gradients += blockIdx.y * n_detectors * n_lookup_neurons_per_detector;
        w_before_detectors_gradients += detector_index * n_lookup_neurons_per_detector;
        double grad_x = static_cast<double>(r_output_gradients[output_index]);
        grad_x *= static_cast<double>(r_weights[output_index]) * lr;
        SUMMATION32_DT sum32_delta = static_cast<SUMMATION32_DT>(grad_x);
        #ifdef ATOMIC
        atomicAdd(
            w_before_detectors_gradients + lookup_index,
            sum32_delta
        );
        #else
        w_before_detectors_gradients[lookup_index] += sum32_delta;
        #endif
    }
}

gather_x_gradients_fully_connected_logic(
    EXTERNAL_REAL_DT* r_weights,
    EXTERNAL_REAL_DT* r_output_gradients,
    int32_t* r_lookup_indices,
    int32_t* r_min_anchor_delta_indices,
    SUMMATION32_DT* w_before_detectors_gradients,
    uint32_t n_outputs,
    uint32_t n_detectors,
    uint32_t n_output_blocks,
    int32_t n_outputs_per_block,
    uint32_t n_lookup_neurons_per_detector,
    double lr,
    double int_rescaler
) {
    uint32_t i = blockIdx.x * blockDim.x + threadIdx.x;
    if(i < n_detectors * n_output_blocks) {
        uint32_t detector_index = i / n_output_blocks;
        uint32_t output_block_index = i % n_output_blocks;
        r_lookup_indices += blockIdx.y * n_detectors;
        int32_t lookup_index = r_lookup_indices[detector_index];
        if(r_min_anchor_delta_indices != nullptr) {
            r_min_anchor_delta_indices += blockIdx.y * n_detectors;
            lookup_index ^= (1 << r_min_anchor_delta_indices[detector_index]);
        }
        uint64_t weight_shift = static_cast<uint64_t>(n_lookup_neurons_per_detector) * n_outputs * detector_index;
        weight_shift += n_outputs * lookup_index + output_block_index * n_outputs_per_block;
        r_weights += weight_shift;

        r_output_gradients += blockIdx.y * n_outputs + output_block_index * n_outputs_per_block;
        w_before_detectors_gradients += blockIdx.y * n_detectors * n_lookup_neurons_per_detector;
        w_before_detectors_gradients += detector_index * n_lookup_neurons_per_detector;
        if((output_block_index == n_output_blocks - 1) && (n_outputs % n_outputs_per_block)) {
            n_outputs_per_block = n_outputs % n_outputs_per_block;
        }
        double grad_x = 0.0;
        double output_grad;
        double weight;
        SUMMATION32_DT sum32_delta;
        for(int32_t cursor=0;cursor < n_outputs_per_block;cursor++) {
             weight = static_cast<double>(r_weights[cursor]);
             output_grad = static_cast<double>(r_output_gradients[cursor]);
             grad_x += weight * output_grad;
        }

        if(fabs(grad_x) > EPS) {
            #ifdef INTEGERS_INSTEAD_OF_FLOATS
                sum32_delta = static_cast<SUMMATION32_DT>(grad_x * lr * static_cast<double>(DENOMINATOR32) * int_rescaler);
                #ifdef ATOMIC
                atomicAdd(
                    w_before_detectors_gradients + lookup_index,
                    sum32_delta
                );
                #else
                w_before_detectors_gradients[lookup_index] += sum32_delta;
                #endif
            #else
                sum32_delta = static_cast<SUMMATION32_DT>(grad_x * lr);
                #ifdef ATOMIC
                atomicAdd(
                    w_before_detectors_gradients + lookup_index,
                    sum32_delta
                );
                #else
                w_before_detectors_gradients[lookup_index] += sum32_delta;
                #endif
            #endif
        }
    }
}

fire_x_gradients_fully_connected_logic(
    EXTERNAL_REAL_DT* r_weights,
    EXTERNAL_REAL_DT* r_output_gradients,
    int32_t* r_lookup_indices,
    int32_t* r_min_anchor_delta_indices,
    SUMMATION32_DT* w_before_detectors_gradients,
    uint32_t n_outputs,
    uint32_t n_detectors,
    uint32_t n_detector_blocks,
    int32_t n_detectors_per_block,
    uint32_t n_lookup_neurons_per_detector,
    double lr,
    double int_rescaler
) {
    uint32_t i = blockIdx.x * blockDim.x + threadIdx.x;
    if(i < n_outputs * n_detector_blocks) {
        uint32_t output_index = i / n_detector_blocks;
        uint32_t detector_block_index = i % n_detector_blocks;
        r_lookup_indices += blockIdx.y * n_detectors;
        r_lookup_indices += detector_block_index * n_detectors_per_block;
        if(r_min_anchor_delta_indices != nullptr) {
            r_min_anchor_delta_indices += blockIdx.y * n_detectors;
            r_min_anchor_delta_indices += detector_block_index * n_detectors_per_block;
        }

        r_output_gradients += blockIdx.y * n_outputs;
        uint64_t weights_per_detector = static_cast<uint64_t>(n_lookup_neurons_per_detector) * n_outputs;
        r_weights += weights_per_detector * detector_block_index * n_detectors_per_block;
        w_before_detectors_gradients += blockIdx.y * n_detectors;
        w_before_detectors_gradients += detector_block_index * n_detectors_per_block;

        int32_t lookup_index;
        double weight;
        SUMMATION32_DT sum32_delta;
        for(int32_t cursor=0;cursor < n_detectors_per_block;cursor++, r_weights+=weights_per_detector) {
            lookup_index = r_lookup_indices[cursor];
            if(r_min_anchor_delta_indices != nullptr) {
                lookup_index ^= (1 << r_min_anchor_delta_indices[cursor]);
            }

            weight = static_cast<double>(r_weights[lookup_index * n_outputs + output_index]);
            #ifdef INTEGERS_INSTEAD_OF_FLOATS
                sum32_delta = static_cast<SUMMATION32_DT>(weight * lr * static_cast<double>(DENOMINATOR32) * int_rescaler);
                #ifdef ATOMIC
                atomicAdd(
                    w_before_detectors_gradients + cursor,
                    sum32_delta
                );
                #else
                w_before_detectors_gradients[cursor] += sum32_delta;
                #endif
            #else
                sum32_delta = static_cast<SUMMATION32_DT>(weight * lr);
                #ifdef ATOMIC
                atomicAdd(
                    w_before_detectors_gradients + cursor,
                    sum32_delta
                );
                #else
                w_before_detectors_gradients[cursor] += sum32_delta;
                #endif
            #endif
        }
    }
}

gather_w_gradients_fully_connected_logic(
    EXTERNAL_REAL_DT* r_output_gradients,
    int32_t* r_lookup_indices,
    EXTERNAL_REAL_DT* w_weights_gradients,
    uint32_t n_outputs,
    uint32_t n_detectors,
    uint32_t n_output_blocks,
    int32_t n_outputs_per_block,
    uint32_t n_lookup_neurons_per_detector,
    double lr,
    double int_rescaler
) {
    uint32_t i = blockIdx.x * blockDim.x + threadIdx.x;
    if(i < n_detectors * n_output_blocks) {
        uint32_t detector_index = i / n_output_blocks;
        uint32_t output_block_index = i % n_output_blocks;
        r_lookup_indices += blockIdx.y * n_detectors;
        int32_t lookup_index = r_lookup_indices[detector_index];
        uint64_t weight_shift = static_cast<uint64_t>(n_lookup_neurons_per_detector) * n_outputs * detector_index;
        weight_shift += n_outputs * lookup_index + output_block_index * n_outputs_per_block;
        w_weights_gradients += weight_shift;
        r_output_gradients += blockIdx.y * n_outputs + output_block_index * n_outputs_per_block;
        if((output_block_index == n_output_blocks - 1) && (n_outputs % n_outputs_per_block)) {
            n_outputs_per_block = n_outputs % n_outputs_per_block;
        }
        double output_grad;
        SUMMATION32_DT sum32_delta;
        for(int32_t cursor=0;cursor < n_outputs_per_block;cursor++) {
            output_grad = static_cast<double>(r_output_gradients[cursor]);

            if(fabs(output_grad) > EPS) {
                #ifdef INTEGERS_INSTEAD_OF_FLOATS
                    sum32_delta = static_cast<SUMMATION32_DT>(output_grad * lr * static_cast<double>(DENOMINATOR32) * int_rescaler);
                    #ifdef ATOMIC
                    atomicAdd(
                        reinterpret_cast<SUMMATION32_DT *>(w_weights_gradients) + cursor,
                        sum32_delta
                    );
                    #else
                    *reinterpret_cast<SUMMATION32_DT *>(w_weights_gradients + cursor) += sum32_delta;
                    #endif
                #else
                    sum32_delta = static_cast<SUMMATION32_DT>(output_grad * lr);
                    #ifdef ATOMIC
                    atomicAdd(
                        w_weights_gradients + cursor,
                        sum32_delta
                    );
                    #else
                    w_weights_gradients[cursor] += sum32_delta;
                    #endif
                #endif
            }
        }
    }
}

gather_gradients_fully_connected_old_logic(
    EXTERNAL_REAL_DT* r_weights,
    EXTERNAL_REAL_DT* r_output_gradients,
    int32_t* r_lookup_indices,
    int32_t* r_min_anchor_delta_indices,
    SUMMATION32_DT* w_before_detectors_gradients,
    EXTERNAL_REAL_DT* w_weights_gradients,
    uint32_t n_outputs,
    uint32_t n_detectors,
    uint32_t n_output_blocks,
    int32_t n_outputs_per_block,
    uint32_t n_lookup_neurons_per_detector,
    double lr,
    double int_rescaler
) {
    uint32_t i = blockIdx.x * blockDim.x + threadIdx.x;
    if(i < n_detectors * n_output_blocks) {
        uint32_t detector_index = i / n_output_blocks;
        uint32_t output_block_index = i % n_output_blocks;
        r_lookup_indices += blockIdx.y * n_detectors;
        int32_t lookup_index = r_lookup_indices[detector_index];
        if(r_min_anchor_delta_indices != nullptr) {
            r_min_anchor_delta_indices += blockIdx.y * n_detectors;
            lookup_index ^= (1 << r_min_anchor_delta_indices[detector_index]);
        }
        uint64_t weight_shift = static_cast<uint64_t>(n_lookup_neurons_per_detector) * n_outputs * detector_index;
        weight_shift += n_outputs * lookup_index + output_block_index * n_outputs_per_block;
        r_weights += weight_shift;
        w_weights_gradients += weight_shift;

        r_output_gradients += blockIdx.y * n_outputs + output_block_index * n_outputs_per_block;
        w_before_detectors_gradients += blockIdx.y * n_detectors * n_lookup_neurons_per_detector;
        w_before_detectors_gradients += detector_index * n_lookup_neurons_per_detector;
        if((output_block_index == n_output_blocks - 1) && (n_outputs % n_outputs_per_block)) {
            n_outputs_per_block = n_outputs % n_outputs_per_block;
        }
        double grad_x = 0.0;
        int32_t n_o_buffered = 0;
        int32_t n_w_buffered = 0;
        float4 o_quad = make_float4(0.0, 0.0, 0.0, 0.0);
        float4 w_quad = make_float4(0.0, 0.0, 0.0, 0.0);
        double output_grad;
        double weight;
        SUMMATION32_DT sum32_delta;
        for(int32_t cursor=0;cursor < n_outputs_per_block;cursor++, r_output_gradients++, r_weights++) {
            if(((reinterpret_cast<uintptr_t>(r_output_gradients) & 15) == 0) && (cursor < n_outputs_per_block - 3)) {
                #ifdef ATOMIC
                asm volatile(
                    "ld.global.v4.f32 {%0,%1,%2,%3}, [%4];"
                    : "=f"(o_quad.x), "=f"(o_quad.y), "=f"(o_quad.z), "=f"(o_quad.w)
                    : "l"(r_output_gradients)
                );
                #else
                o_quad = *reinterpret_cast<float4 *>(r_output_gradients);
                #endif
                output_grad = static_cast<double>(o_quad.x);
                n_o_buffered = 3;
            } else if(n_o_buffered > 0) {
                if(n_o_buffered == 3) {
                    output_grad = static_cast<double>(o_quad.y);
                } else if(n_o_buffered == 2) {
                    output_grad = static_cast<double>(o_quad.z);
                } else {
                    output_grad = static_cast<double>(o_quad.w);
                }
                n_o_buffered--;
            } else {
                output_grad = static_cast<double>(*r_output_gradients);
            }

            if(((reinterpret_cast<uintptr_t>(r_weights) & 15) == 0) && (cursor < n_outputs_per_block - 3)) {
                #ifdef ATOMIC
                asm volatile(
                    "ld.global.v4.f32 {%0,%1,%2,%3}, [%4];"
                    : "=f"(w_quad.x), "=f"(w_quad.y), "=f"(w_quad.z), "=f"(w_quad.w)
                    : "l"(r_weights)
                );
                #else
                w_quad = *reinterpret_cast<float4 *>(r_weights);
                #endif
                weight = static_cast<double>(w_quad.x);
                n_w_buffered = 3;
            } else if(n_w_buffered > 0) {
                if(n_w_buffered == 3) {
                    weight = static_cast<double>(w_quad.y);
                } else if(n_w_buffered == 2) {
                    weight = static_cast<double>(w_quad.z);
                } else {
                    weight = static_cast<double>(w_quad.w);
                }
                n_w_buffered--;
            } else {
                weight = static_cast<double>(*r_weights);
            }

            if(fabs(output_grad) > EPS) {
                grad_x += weight * output_grad;
                #ifdef INTEGERS_INSTEAD_OF_FLOATS
                    sum32_delta = static_cast<SUMMATION32_DT>(output_grad * lr * static_cast<double>(DENOMINATOR32) * int_rescaler);
                    #ifdef ATOMIC
                    atomicAdd(
                        reinterpret_cast<SUMMATION32_DT *>(w_weights_gradients) + cursor,
                        sum32_delta
                    );
                    #else
                    *reinterpret_cast<SUMMATION32_DT *>(w_weights_gradients + cursor) += sum32_delta;
                    #endif
                #else
                    sum32_delta = static_cast<SUMMATION32_DT>(output_grad * lr);
                    #ifdef ATOMIC
                    atomicAdd(
                        w_weights_gradients + cursor,
                        sum32_delta
                    );
                    #else
                    w_weights_gradients[cursor] += sum32_delta;
                    #endif
                #endif
            }
        }

        #ifdef INTEGERS_INSTEAD_OF_FLOATS
            sum32_delta = static_cast<SUMMATION32_DT>(grad_x * lr * static_cast<double>(DENOMINATOR32) * int_rescaler);
            #ifdef ATOMIC
            atomicAdd(
                w_before_detectors_gradients + lookup_index,
                sum32_delta
            );
            #else
            w_before_detectors_gradients[lookup_index] += sum32_delta;
            #endif
        #else
            sum32_delta = static_cast<SUMMATION32_DT>(grad_x * lr);
            #ifdef ATOMIC
            atomicAdd(
                w_before_detectors_gradients + lookup_index,
                sum32_delta
            );
            #else
            w_before_detectors_gradients[lookup_index] += sum32_delta;
            #endif
        #endif
    }
}

propagate_through_detectors_logic(
    int32_t* r_lookup_indices,
    EXTERNAL_REAL_DT* r_min_anchor_deltas,
    int32_t* r_min_anchor_delta_indices,
    uint32_t n_detectors,
    uint32_t n_anchors_per_detector,
    AnchorsPair* r_detectors,
    uint32_t n_lookup_neurons_per_detector,
    SUMMATION32_DT* rw_before_detectors_gradients,
    EXTERNAL_REAL_DT* w_input_gradients,
    uint32_t n_inputs,
    double int_rescaler
) {
    uint32_t i = blockIdx.x * blockDim.x + threadIdx.x;

    if(i < n_detectors) {
        uint64_t shift = blockIdx.y * n_detectors;
        r_lookup_indices += shift;
        r_min_anchor_deltas += shift;
        r_min_anchor_delta_indices += shift;
        rw_before_detectors_gradients += shift * n_lookup_neurons_per_detector;
        rw_before_detectors_gradients += i * n_lookup_neurons_per_detector;
        w_input_gradients += blockIdx.y * n_inputs;

        int32_t lookup_index = r_lookup_indices[i];
        int32_t min_anchor_delta_index = r_min_anchor_delta_indices[i];
        int32_t alt_lookup_neuron_idx = lookup_index ^ (1 << min_anchor_delta_index);

        double du = static_cast<double>(r_min_anchor_deltas[i]);
        if(du > 0) {
            du = 1.0 / (1.0 + fabs(du));
            du *= 0.5 * du;
        } else {
            du = 1.0 / (1.0 + fabs(du));
            du *= -0.5 * du;
        }
        du *= static_cast<double>(rw_before_detectors_gradients[lookup_index]) - static_cast<double>(rw_before_detectors_gradients[alt_lookup_neuron_idx]);

        if(fabs(du) > EPS) {
            AnchorsPair ap = r_detectors[i * n_anchors_per_detector + min_anchor_delta_index];
        
            #ifdef INTEGERS_INSTEAD_OF_FLOATS
                SUMMATION32_DT sum32_delta = static_cast<SUMMATION32_DT>(du * static_cast<double>(DENOMINATOR32) * int_rescaler);
                #ifdef ATOMIC
                atomicAdd(
                    reinterpret_cast<SUMMATION32_DT *>(w_input_gradients) + ap.anchor1_id,
                    sum32_delta
                );
                atomicAdd(
                    reinterpret_cast<SUMMATION32_DT *>(w_input_gradients) + ap.anchor2_id,
                    -sum32_delta
                );
                #else
                *reinterpret_cast<SUMMATION32_DT *>(w_input_gradients + ap.anchor1_id) += sum32_delta;
                *reinterpret_cast<SUMMATION32_DT *>(w_input_gradients + ap.anchor2_id) -= sum32_delta;
                #endif
            #else
                #ifdef ATOMIC
                atomicAdd(
                    w_input_gradients + ap.anchor1_id,
                    du
                );
                atomicAdd(
                    w_input_gradients + ap.anchor2_id,
                    -du
                );
                #else
                w_input_gradients[ap.anchor1_id] += du;
                w_input_gradients[ap.anchor2_id] -= du;
                #endif
            #endif
        }
        __SUPER_DETAILED_TRACE__(
            "[propagate_through_detectors_logic] batch_index %d, lookup_index %d, alt_lookup_neuron_idx %d\n",
            blockIdx.y, lookup_index, alt_lookup_neuron_idx
        );
        rw_before_detectors_gradients[lookup_index] = 0.0;
        rw_before_detectors_gradients[alt_lookup_neuron_idx] = 0.0;
    }
}

convert_integers_to_floats_logic(
    EXTERNAL_REAL_DT* buffer,
    uint64_t n,
    double int_rescaler
) {
    uint64_t i = static_cast<uint64_t>(blockIdx.x) * blockDim.x + threadIdx.x;
    if(i < n) {
        #ifdef INTEGERS_INSTEAD_OF_FLOATS
        buffer += n * blockIdx.y;
        double y = static_cast<double>(*reinterpret_cast<SUMMATION32_DT *>(buffer + i)) * static_cast<double>(DENOMINATOR32_RECIPROC);
        buffer[i] = static_cast<EXTERNAL_REAL_DT>(y / int_rescaler);
        #endif
    }
}

// Kernel for checking detectors across a sequence of timesteps
// Grid: (n_detector_items blocks, batch_size batches) where n_detector_items = sequence_length * n_detectors
// Each thread processes one detector at one timestep
check_detectors_for_sequence_logic(
    EXTERNAL_REAL_DT* r_input,
    uint32_t n_inputs,
    uint32_t sequence_length,
    AnchorsPair* r_detectors,
    uint32_t n_detectors,
    uint32_t n_anchors_per_detector,
    int32_t* w_lookup_indices,
    EXTERNAL_REAL_DT* w_min_anchor_deltas,
    int32_t* w_min_anchor_delta_indices
) {
    // i represents n_detectors * timestep + detector_index
    uint32_t i = blockIdx.x * blockDim.x + threadIdx.x;
    uint32_t n_detector_items = sequence_length * n_detectors;
    
    if(i < n_detector_items) {
        uint32_t timestep = i / n_detectors;
        uint32_t detector_index = i % n_detectors;

        // Offset input for current batch and timestep
        // Input layout: [batch0_t0, batch0_t1, ..., batch0_tN, batch1_t0, ...]
        r_input += blockIdx.y * n_inputs * sequence_length + timestep * n_inputs;
        w_lookup_indices += blockIdx.y * n_detector_items + i;
        w_min_anchor_deltas += blockIdx.y * n_detector_items + i;
        w_min_anchor_delta_indices += blockIdx.y * n_detector_items + i;
        
        // Get detector's anchor pairs
        AnchorsPair* detector_anchors = r_detectors + detector_index * n_anchors_per_detector;
        
        // Calculate differences between anchor pairs and form bit representation
        int32_t lookup_index = 0;
        EXTERNAL_REAL_DT min_delta = 0.0;
        int32_t min_delta_anchor_idx = -1;
        AnchorsPair ap;
        
        for(uint32_t anchor_idx = 0; anchor_idx < n_anchors_per_detector; anchor_idx++) {
            ap = detector_anchors[anchor_idx];
            
            // Skip if anchor IDs are invalid
            if(ap.anchor1_id == std::numeric_limits<NeuronIndex_t>::max() ||
               ap.anchor2_id == std::numeric_limits<NeuronIndex_t>::max()) {
                continue;
            }
            
            EXTERNAL_REAL_DT delta = r_input[ap.anchor1_id] - r_input[ap.anchor2_id];
            
            // Form bit representation: 1 if delta > 0, 0 if delta <= 0
            if(delta > 0.0) {
                lookup_index |= (1 << anchor_idx);
            }
            
            // Track anchor pair with minimum absolute delta
            if((min_delta_anchor_idx == -1) || (fabs(delta) < fabs(min_delta))) {
                min_delta = delta;
                min_delta_anchor_idx = anchor_idx;
            }
        }
        
        // Store lookup index and related data
        *w_lookup_indices = lookup_index;
        *w_min_anchor_deltas = min_delta;
        *w_min_anchor_delta_indices = min_delta_anchor_idx;
        
        __SUPER_DETAILED_TRACE__("check_detectors_for_sequence_logic: batch_index %d, detector_index %d, timestep %d, lookup_index %d, min_delta %f, min_delta_anchor_idx %d\n", blockIdx.y, detector_index, timestep, lookup_index, min_delta, min_delta_anchor_idx);
    }
}

check_positional_embeddings_logic(
    uint32_t sequence_length,
    EXTERNAL_REAL_DT* r_positional_embeddings,
    uint32_t n_detectors,
    uint32_t positional_dim,
    int32_t* w_positional_lookup_indices,
    EXTERNAL_REAL_DT* w_positional_min_deltas,
    int32_t* w_positional_min_delta_indices
) {
    // i represents n_detectors * timestep + detector_index
    uint32_t i = blockIdx.x * blockDim.x + threadIdx.x;
    uint32_t n_detector_items = sequence_length * n_detectors;
    
    if(i < n_detector_items) {
        uint32_t timestep = i / n_detectors;
        uint32_t detector_index = i % n_detectors;
        
        // Calculate offset for this batch, detector, and timestep
        // blockIdx.y will be 0 because positional embeddings are not batched but we leave it here for generality
        uint32_t batch_offset = blockIdx.y * n_detector_items;
        w_positional_lookup_indices += batch_offset + i;
        w_positional_min_deltas += batch_offset + i;
        w_positional_min_delta_indices += batch_offset + i;
        
        // Get positional embedding for this detector and timestep
        // Positional embeddings are stored as [sequence_length * n_detectors * positional_dim]
        uint32_t embedding_offset = timestep * n_detectors * positional_dim + detector_index * positional_dim;
        EXTERNAL_REAL_DT* current_embedding = r_positional_embeddings + embedding_offset;
        
        int32_t lookup_index = 0;
        int32_t min_dim_index = -1;
        EXTERNAL_REAL_DT min_delta;
        EXTERNAL_REAL_DT embedding_value;
        
        for(uint32_t dim = 0; dim < positional_dim; dim++) {
            embedding_value = current_embedding[dim];
            
            if(embedding_value > 0.0) {
                lookup_index |= (1 << dim);
            }
            
            if((min_dim_index == -1) || (fabs(embedding_value) < fabs(min_delta))) {
                min_delta = embedding_value;
                min_dim_index = dim;
            }
        }
        
        *w_positional_lookup_indices = lookup_index;
        *w_positional_min_deltas = min_delta;
        *w_positional_min_delta_indices = min_dim_index;
        
        __SUPER_DETAILED_TRACE__(
            "check_positional_embeddings_logic: batch_index %d, detector_index %d, timestep %d, lookup_index %d, min_dim_index %d, min_delta %f\n",
            blockIdx.y, detector_index, timestep, lookup_index, min_dim_index, min_delta
        );
    }
}

// TODO add alternatives firing buffer

densify_firing_stat_logic(
    EXTERNAL_REAL_DT* r_firing_stat,
    NeuronShiftFiring* rw_firings_buffer,
    uint64_t* rw_firings_counter_ptr,
    uint32_t n_lookup_neurons,
    uint32_t sequence_length,
    int device
) {
    uint32_t tid = threadIdx.x;
    uint32_t neuron_index = blockIdx.x * blockDim.x + tid;
    
    // Check bounds
    if(neuron_index < n_lookup_neurons) {
        // blockIdx.y encodes: batch_index * sequence_length + timestep
        // Extract batch_index and timestep
        uint32_t batch_index = blockIdx.y / sequence_length;
        uint32_t timestep = blockIdx.y % sequence_length;

        // Calculate index into firing_stat array
        // firing_stat layout: [neuron0_batch0_t0, neuron0_batch0_t1, ..., neuron0_batch0_tN, neuron0_batch1_t0, ...]
        // Layout: [batch0_t0_neuron0, batch0_t0_neuron1, ..., batch0_t0_neuronN, batch0_t1_neuron0, ...]
        uint32_t firing_stat_index = timestep * n_lookup_neurons + neuron_index;
        EXTERNAL_REAL_DT firing_value = r_firing_stat[firing_stat_index];

        if(device == -1) {
            // CPU path
            if(firing_value != 0.0) {
                uint64_t firings_offset = *rw_firings_counter_ptr;
                (*rw_firings_counter_ptr)++;

                rw_firings_buffer[firings_offset] = NeuronShiftFiring{
                    batch_index,  // batch_index
                    firing_value,  // payload (value from firing_stat)
                    neuron_index,  // neuron_id
                    timestep  // shift (timestep)
                };
            }
        } else {
            #ifdef ATOMIC
            // Shared memory for reduction
            extern __shared__ __align__(16) uint8_t __sm[];
            uint32_t *sdata = reinterpret_cast<uint32_t *>(__sm);

            sdata[tid] = (firing_value != 0.0) ? 1 : 0;
            __syncthreads();

            uint32_t t;
            int offset;
            int idx;

            // upsweep (reduce)
            for(offset = 1; offset < blockDim.x; offset <<= 1) {
                idx = ((tid + 1) * (offset << 1)) - 1;
                if(idx < blockDim.x) {
                    t = sdata[idx - offset];
                    if(t > 0) {
                        sdata[idx] += t;
                    }
                }
                __syncthreads();
            }

            // inject global shift
            if(tid == 0) {
                sdata[blockDim.x - 1] = atomicAdd(
                    reinterpret_cast<unsigned long long*>(rw_firings_counter_ptr),
                    static_cast<unsigned long long>(sdata[blockDim.x - 1])
                );
            }
            __syncthreads();

            // downsweep
            for(offset = blockDim.x >> 1; offset > 0; offset >>= 1) {
                idx = ((tid + 1) * (offset << 1)) - 1;
                if (idx < blockDim.x) {
                    t = sdata[idx - offset];
                    sdata[idx - offset] = sdata[idx];
                    if(t > 0) {
                        sdata[idx] += t;
                    }
                }
                __syncthreads();
            }

            // Write firing if this neuron fired
            if(firing_value != 0.0) {
                rw_firings_buffer[sdata[tid]] = NeuronShiftFiring{
                    batch_index,  // batch_index
                    firing_value,  // payload (value from firing_stat)
                    neuron_index,  // neuron_id
                    timestep  // shift (timestep)
                };
            }
            #endif
        }
    }
}


// TODO
// Kernel for filling outputs based on lookup indices (for sequence processing)
// Similar to fill_outputs_by_forward_groups_logic but works with Firing entries from densify
// Grid: (n_firings blocks, max_forward_groups_per_neuron batches)
fill_outputs_by_lookup_indices_logic(
    EXTERNAL_REAL_DT* r_weights,
    NeuronDataId_t first_synapse_id,
    NeuronShiftFiring* r_firings_buffer,
    uint64_t n_firings,
    EXTERNAL_REAL_DT* w_output,
    uint32_t n_lookup_neurons,
    uint32_t n_outputs,
    NoDelaysIndexedSynapsesInfo* r_lookup_neuron_synapses_infos,
    uint32_t synapse_group_size,
    uint8_t* lut_data,
    double int_rescaler
) {
    uint64_t i = static_cast<uint64_t>(blockIdx.x) * blockDim.x + threadIdx.x;
    
    if(i < n_firings) {
        NeuronShiftFiring firing = r_firings_buffer[i];
        
        // Calculate batch and sequence indices from firing.batch_index
        // firing.batch_index encodes: batch_index * sequence_length + timestep
        // We need sequence_length to decode this - it should be passed as a parameter
        // For now, assuming it's encoded in the batch_index or we extract it differently
        
        // Offset w_output for current batch and timestep
        // This needs sequence_length to be passed as a parameter
        // For now, using a simplified approach - firing.batch_index directly indexes into output
        w_output += firing.batch_index * n_outputs;
        
        // Get synapse info for this firing using neuron_id
        // neuron_id is the lookup neuron index
        NoDelaysIndexedSynapsesInfo lookup_neuron_synapses_info = r_lookup_neuron_synapses_infos[firing.neuron_id];
        
        // Use the first group for this neuron
        if(lookup_neuron_synapses_info.n_groups > 0 && lookup_neuron_synapses_info.n_synapse_metas == 1) {
            NeuronDataId_t current_group_id = lookup_neuron_synapses_info.first_group_id;
            ForwardSynapseGroup* fws_group_ptr = GetForwardSynapseGroup(current_group_id, lut_data);
            int32_t current_group_size = static_cast<int32_t>(SynapseGroupSize(fws_group_ptr->meta_info));
            NeuronIndex_t* current_target_ptr = TargetNeuronsInForwardGroup(current_group_id, lut_data);
            long weight_shift = (reinterpret_cast<uint8_t *>(current_target_ptr) - (lut_data + first_synapse_id)) >> 2;
            EXTERNAL_REAL_DT* weight_ptr = r_weights + weight_shift;
            
            uint4 targets_quad = make_uint4(0, 0, 0, 0);
            float4 weights_quad = make_float4(0.0, 0.0, 0.0, 0.0);
            
            NeuronIndex_t target_id;
            double weight;
            SUMMATION32_DT payload;
            
            int32_t n_t_buffered = 0;
            int32_t n_w_buffered = 0;
            
            // Scale weight by firing value
            EXTERNAL_REAL_DT firing_weight = firing.payload;
            
            for(int32_t cursor = 0; cursor < current_group_size; cursor++, current_target_ptr++, weight_ptr++) {
            if(((reinterpret_cast<uintptr_t>(current_target_ptr) & 15) == 0) && (cursor < current_group_size - 3)) {
                #ifdef ATOMIC
                asm volatile(
                    "ld.global.v4.s32 {%0,%1,%2,%3}, [%4];"
                    : "=r"(targets_quad.x), "=r"(targets_quad.y), "=r"(targets_quad.z), "=r"(targets_quad.w)
                    : "l"(current_target_ptr)
                );
                #else
                targets_quad = *reinterpret_cast<uint4 *>(current_target_ptr);
                #endif
                target_id = targets_quad.x;
                n_t_buffered = 3;
            } else if(n_t_buffered > 0) {
                if(n_t_buffered == 3) {
                    target_id = targets_quad.y;
                } else if(n_t_buffered == 2) {
                    target_id = targets_quad.z;
                } else {
                    target_id = targets_quad.w;
                }
                n_t_buffered--;
            } else {
                target_id = *current_target_ptr;
            }
            
            if(((reinterpret_cast<uintptr_t>(weight_ptr) & 15) == 0) && (cursor < current_group_size - 3)) {
                #ifdef ATOMIC
                asm volatile(
                    "ld.global.v4.f32 {%0,%1,%2,%3}, [%4];"
                    : "=f"(weights_quad.x), "=f"(weights_quad.y), "=f"(weights_quad.z), "=f"(weights_quad.w)
                    : "l"(weight_ptr)
                );
                #else
                weights_quad = *reinterpret_cast<float4 *>(weight_ptr);
                #endif
                weight = static_cast<double>(weights_quad.x) * static_cast<double>(firing_weight);
                n_w_buffered = 3;
            } else if(n_w_buffered > 0) {
                if(n_w_buffered == 3) {
                    weight = static_cast<double>(weights_quad.y) * static_cast<double>(firing_weight);
                } else if(n_w_buffered == 2) {
                    weight = static_cast<double>(weights_quad.z) * static_cast<double>(firing_weight);
                } else {
                    weight = static_cast<double>(weights_quad.w) * static_cast<double>(firing_weight);
                }
                n_w_buffered--;
            } else {
                weight = static_cast<double>(*weight_ptr) * static_cast<double>(firing_weight);
            }
            
            target_id -= n_lookup_neurons;
            
            #ifdef INTEGERS_INSTEAD_OF_FLOATS
                payload = static_cast<SUMMATION32_DT>(weight * static_cast<double>(DENOMINATOR32) * int_rescaler);
                #ifdef ATOMIC
                atomicAdd(
                    reinterpret_cast<SUMMATION32_DT *>(w_output + target_id),
                    payload
                );
                #else
                *reinterpret_cast<SUMMATION32_DT *>(w_output + target_id) += payload;
                #endif
            #else
                payload = static_cast<SUMMATION32_DT>(weight);
                #ifdef ATOMIC
                atomicAdd(
                    w_output + target_id,
                    payload
                );
                #else
                w_output[target_id] += payload;
                #endif
            #endif
            }
        }
    }
}

fill_after_detectors_firing_stat_logic(
    int32_t* r_lookup_indices,
    int32_t* r_positional_lookup_indices,
    uint32_t sequence_length,
    uint32_t n_detectors,
    uint32_t n_anchors_per_detector,
    uint32_t positional_dim,
    uint32_t n_lookup_neurons,
    EXTERNAL_REAL_DT* w_firing_stat
) {
    // Calculate grid dimensions for tiles
    uint32_t grid_x = (sequence_length + TILE - 1) / TILE;
    uint32_t grid_y = (sequence_length + TILE - 1) / TILE;
    uint32_t total_tiles = grid_x * grid_y;
    
    // blockIdx.x encodes: tile_id * n_detectors + detector_index
    // Extract tile_id and detector_index
    uint32_t tile_id = blockIdx.x / n_detectors;
    uint32_t detector_index = blockIdx.x % n_detectors;
    
    if(tile_id >= total_tiles) {
        return;
    }
    
    uint32_t bi = tile_id / grid_x;  // tile row
    uint32_t bj = tile_id % grid_x;  // tile col
    
    // Skip tiles below diagonal (bi > bj means all pairs have i > j, which we don't want)
    if(bi > bj) {
        return;
    }
    
    uint32_t batch_index = blockIdx.y;
    
    // Calculate tile start positions
    uint32_t i0 = bi * TILE;
    uint32_t j0 = bj * TILE;
    
    // Get thread coordinates within tile
    uint32_t i = i0 + threadIdx.y;  // first timestep (row)
    uint32_t j = j0 + threadIdx.x;  // second timestep (col)
    
    // Check if this (i, j) pair is valid (i < j and within bounds)
    bool valid_pair = false;
    if(bi < bj) {
        // Tile strictly above diagonal - all elements are valid (i < j)
        if(i < sequence_length && j < sequence_length) {
            valid_pair = true;
        }
    } else {
        // Diagonal tile - only process where i < j
        if(i < sequence_length && j < sequence_length && i < j) {
            valid_pair = true;
        }
    }
    
    if(valid_pair) {
        // Calculate offsets into lookup_indices arrays
        // Layout: [batch0_t0_det0, batch0_t0_det1, ..., batch0_t0_detN, batch0_t1_det0, ...]
        uint32_t n_detector_items = sequence_length * n_detectors;
        uint32_t batch_offset = batch_index * n_detector_items;
        
        // Get Q: lookup index from first timestep (i)
        uint32_t q_offset = batch_offset + j * n_detectors + detector_index;
        int32_t Q = r_lookup_indices[q_offset];
        
        // Get P: lookup index from second timestep (j)
        uint32_t p_offset = batch_offset + j * n_detectors + detector_index;
        int32_t P = r_lookup_indices[p_offset];
        
        // Get PE: positional lookup index from first timestep (i)
        uint32_t pe_offset = i * n_detectors + detector_index;
        int32_t PE = r_positional_lookup_indices[pe_offset];
        
        uint32_t concatenated_index = (
            (Q << (n_anchors_per_detector + positional_dim)) | 
            (P << positional_dim) | 
            PE
        );
        
        // Calculate index into w_firing_stat
        // w_firing_stat layout: [neuron0_batch0_t0, neuron0_batch0_t1, ..., neuron0_batch0_tN, neuron0_batch1_t0, ...]
        // Layout: [batch0_t0_neuron0, batch0_t0_neuron1, ..., batch0_t0_neuronN, batch0_t1_neuron0, ...]
        // Index = batch_index * n_lookup_neurons * sequence_length + i * n_lookup_neurons + concatenated_index
        uint32_t firing_stat_index = batch_index * n_lookup_neurons * sequence_length + 
                                        j * n_lookup_neurons + 
                                        concatenated_index;
        
        #ifdef ATOMIC
        atomicAdd(
            w_firing_stat + firing_stat_index,
            1.0
        );
        #else
        w_firing_stat[firing_stat_index] += 1.0;
        #endif

        // TODO add alternative indices as negatives

        __SUPER_DETAILED_TRACE__(
            "fill_after_detectors_firing_stat_logic: batch_index %d, i %d, j %d, detector_index %d, Q %d, P %d, PE %d, concat %d\n", 
            batch_index, i, j, detector_index, Q, P, PE, concatenated_index
        );
    }
}
